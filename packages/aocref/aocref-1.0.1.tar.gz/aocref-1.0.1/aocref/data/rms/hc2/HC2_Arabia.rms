/* ************ HC2_Arabia ************ */

/* map made by AlgernonR for HiddenCup2 */

/* this version of arabia has always grassy terrain,
 * the elevation is lowered, woodlines more consistent,
 * and when played as Regicide the players will have a king and barracks,
 * but not the additional resources, villagers nor a castle at start. */

/* all HiddenCup maps are carefully edited to prevent bugged resources */
/* in all HiddenCup maps each player starts with one sheep controlled from the start */ 

#const POP_HEADROOM 4
#const MOD_RESOURCE 1
#const ATTR_SET 0
#const STARTING_FOOD 91
#const STARTING_WOOD 92
#const STARTING_STONE 93
#const STARTING_GOLD 94


<PLAYER_SETUP>
random_placement

if REGICIDE
effect_amount MOD_RESOURCE POP_HEADROOM ATTR_SET 5

effect_amount MOD_RESOURCE STARTING_GOLD ATTR_SET 100
effect_amount MOD_RESOURCE STARTING_STONE ATTR_SET 50
effect_amount MOD_RESOURCE STARTING_FOOD ATTR_SET -300
effect_amount MOD_RESOURCE STARTING_WOOD ATTR_SET -300
endif


effect_amount SET_ATTRIBUTE MAP_REVEALER ATTR_LINE_OF_SIGHT 0

ai_info_map_type ARABIA 0 0 0 

<LAND_GENERATION>

base_terrain GRASS

create_player_lands
{
  terrain_type GRASS
  land_percent 25
  base_size 9
  right_border 1
  top_border 1
  left_border 1
  bottom_border 1
}

<ELEVATION_GENERATION>

create_elevation 4
{
  base_terrain GRASS
  number_of_clumps 12
  number_of_tiles 2800
  set_scale_by_groups
  set_scale_by_size
}


<CLIFF_GENERATION>
min_number_of_cliffs 4
max_number_of_cliffs 10
min_length_of_cliff 3
max_length_of_cliff 7


<TERRAIN_GENERATION>

create_terrain FOREST
{
  base_terrain GRASS
  spacing_to_other_terrain_types 5
  land_percent 11
  number_of_clumps 11
  set_avoid_player_start_areas
  set_scale_by_groups
}

create_terrain GRASS3
{
  base_terrain GRASS
  number_of_clumps 16
  spacing_to_other_terrain_types 0
  land_percent 10
  set_scale_by_size
}

create_terrain FOREST
{
  base_terrain GRASS
  spacing_to_other_terrain_types 3
  land_percent 1
  number_of_clumps 3
  set_avoid_player_start_areas
  set_scale_by_groups
}

create_terrain LEAVES
{
  base_terrain GRASS
  number_of_clumps 24
  spacing_to_other_terrain_types 1
  land_percent 2
  set_scale_by_size
}

create_terrain GRASS2
{
  base_terrain GRASS
  number_of_clumps 30
  spacing_to_other_terrain_types 1
  land_percent 2
  set_scale_by_size
}


<OBJECTS_GENERATION>

create_object OAKTREE
{
number_of_objects 2
set_gaia_object_only
set_place_for_every_player
min_distance_to_players 4
max_distance_to_players 5
min_distance_group_placement 2
}

create_object OAKTREE
{
number_of_objects 3
set_gaia_object_only
set_place_for_every_player
min_distance_to_players 6
max_distance_to_players 8
min_distance_group_placement 2
}

create_object OAKTREE
{
number_of_objects 30
set_gaia_object_only
set_scaling_to_map_size
min_distance_to_players 20
}

create_object TOWN_CENTER
{
set_place_for_every_player
group_placement_radius 18
min_distance_to_players 0
max_distance_to_players 0
}

create_object VILLAGER
{
set_place_for_every_player
min_distance_to_players 6
max_distance_to_players 7
}

create_object SCOUT
{
number_of_objects 1
set_place_for_every_player
min_distance_to_players 8
max_distance_to_players 9
}

if REGICIDE
create_object BARRACKS
{
number_of_objects 1
set_place_for_every_player
min_distance_to_players 10
max_distance_to_players 12
}
create_object KING
{
set_place_for_every_player
min_distance_to_players 6
max_distance_to_players 7
}
endif

create_object SHEEP
{
number_of_objects 1
set_place_for_every_player
min_distance_to_players 9
max_distance_to_players 10
}

create_object SHEEP
{
number_of_objects 3
set_loose_grouping
set_gaia_object_only
set_place_for_every_player
min_distance_to_players 9
max_distance_to_players 10
}

create_object SHEEP
{
number_of_objects 2
number_of_groups  2
set_loose_grouping
set_gaia_object_only
set_place_for_every_player
min_distance_to_players 14
max_distance_to_players 26
min_distance_group_placement 3
}

create_object DEER
{
number_of_objects 4
group_placement_radius 3
set_loose_grouping
set_gaia_object_only
set_place_for_every_player
min_distance_to_players 18
max_distance_to_players 18
}

create_object BOAR
{
number_of_objects 1
set_loose_grouping
set_gaia_object_only
set_place_for_every_player
min_distance_to_players 16
max_distance_to_players 21
min_distance_group_placement 3
}

create_object BOAR
{
number_of_objects 1
set_gaia_object_only
set_place_for_every_player
min_distance_to_players 16
max_distance_to_players 22
min_distance_group_placement 3
}

create_object MAP_REVEALER
{
terrain_to_place_on FOREST
min_distance_group_placement 3
number_of_objects 999
set_gaia_object_only
}

create_object FORAGE
{
number_of_objects 6
group_placement_radius 3
set_tight_grouping
set_gaia_object_only
set_place_for_every_player
min_distance_to_players 10
max_distance_to_players 13
min_distance_group_placement 5
}

create_object GOLD
{
number_of_objects 7
group_placement_radius 3
set_tight_grouping
set_gaia_object_only
set_place_for_every_player
min_distance_to_players 13
max_distance_to_players 16
min_distance_group_placement 8
max_distance_to_other_zones 7
}

create_object GOLD
{
number_of_objects 4
group_placement_radius 3
set_tight_grouping
set_gaia_object_only
set_place_for_every_player
min_distance_to_players 19
max_distance_to_players 24
min_distance_group_placement 9
max_distance_to_other_zones 7
}

create_object GOLD
{
number_of_objects 4
group_placement_radius 3
set_tight_grouping
set_gaia_object_only
set_place_for_every_player
min_distance_to_players 25
max_distance_to_players 30
min_distance_group_placement 9
max_distance_to_other_zones 7
}

create_object STONE
{
number_of_objects 5
group_placement_radius 3
set_tight_grouping
set_gaia_object_only
set_place_for_every_player
min_distance_to_players 14
max_distance_to_players 19
min_distance_group_placement 8
max_distance_to_other_zones 7
}

create_object STONE
{
number_of_objects 4
group_placement_radius 3
set_tight_grouping
set_gaia_object_only
set_place_for_every_player
min_distance_to_players 22
max_distance_to_players 29
min_distance_group_placement 9
max_distance_to_other_zones 7
}

create_object GOLD
{
number_of_objects 3
number_of_groups 2
group_placement_radius 2
set_tight_grouping
set_gaia_object_only
min_distance_to_players 45
set_scaling_to_map_size
temp_min_distance_group_placement 20
min_distance_group_placement 12
}

create_object STONE
{
number_of_objects 3
number_of_groups 1
group_placement_radius 2
set_tight_grouping
set_gaia_object_only
min_distance_to_players 45
set_scaling_to_map_size
temp_min_distance_group_placement 30
min_distance_group_placement 12
}

create_object WOLF
{
number_of_objects 2
set_gaia_object_only
set_place_for_every_player
min_distance_to_players 32
max_distance_to_players 45
temp_min_distance_group_placement 12
}

create_object WOLF
{
number_of_groups 2
number_of_objects 1
set_loose_grouping
set_scaling_to_map_size
set_gaia_object_only
temp_min_distance_group_placement 20
min_distance_to_players 40
}

create_object RELIC
{
number_of_objects 5
min_distance_to_players 25
min_distance_group_placement 5
temp_min_distance_group_placement 20
}

create_object HAWK
{
  number_of_objects 4
  set_gaia_object_only
  set_scaling_to_map_size
}
