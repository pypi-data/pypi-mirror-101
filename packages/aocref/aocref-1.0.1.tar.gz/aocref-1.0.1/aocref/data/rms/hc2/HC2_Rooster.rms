/* ************ HC2_Rooster_v2 ************ */

/* map Derived from LightRandom (LR), adjusted for  HiddenCup2	*/

/* edited by HenkDeSuperNerd */

/* all HiddenCup maps are carefully edited to prevent bugged resources */
/* in all HiddenCup maps each player starts with one sheep controlled from the start */


/* ************************************ */
/* Rooster (LR) made by The_Prophet */

#const ATTR_LINE_OF_SIGHT 1

<PLAYER_SETUP>
  random_placement

effect_amount SET_ATTRIBUTE MAP_REVEALER ATTR_LINE_OF_SIGHT 0

  
  
/* ****************************************************** */

<LAND_GENERATION>

base_terrain WATER

create_player_lands
{
     terrain_type GRASS
     land_percent 100
     base_size 14
     border_fuzziness 20
     clumping_factor 20
left_border 5
bottom_border 5
top_border 5
right_border 5
     other_zone_avoidance_distance 15
}

create_land
{
  terrain_type WATER
  land_percent 100
  right_border 36
  left_border 36
  top_border 36
  bottom_border 36
  border_fuzziness 15
  other_zone_avoidance_distance 0
  clumping_factor 15
}

<TERRAIN_GENERATION>

create_terrain PINE_FOREST
{
     base_terrain GRASS
     spacing_to_other_terrain_types 4
     land_percent 12
     number_of_clumps 30
     clumping_factor 100
     set_avoid_player_start_areas
     set_scale_by_groups
}

/* Mixing Water */

create_terrain MED_WATER
{
     base_terrain WATER
     number_of_clumps 10
     spacing_to_other_terrain_types 2
     land_percent 70
}

create_terrain MED_WATER
{
     base_terrain WATER
     number_of_clumps 30
     spacing_to_other_terrain_types 1
     land_percent 1
}

create_terrain DEEP_WATER
{
     base_terrain MED_WATER
     number_of_clumps 8
     spacing_to_other_terrain_types 3
     land_percent 40
}

create_terrain DEEP_WATER
{
     base_terrain MED_WATER
     number_of_clumps 30
     spacing_to_other_terrain_types 1
     land_percent 1
}

create_terrain MED_WATER
{
     base_terrain DEEP_WATER
     number_of_clumps 30
     spacing_to_other_terrain_types 1
     land_percent 1
}

create_terrain WATER
{
     base_terrain MED_WATER
     number_of_clumps 30
     spacing_to_other_terrain_types 1
     land_percent 1
}

<OBJECTS_GENERATION>

/* ********************************************************************** */


create_object TOWN_CENTER
{
  set_place_for_every_player
  group_placement_radius     18
  min_distance_to_players    0
  max_distance_to_players    0
}

create_object OAKTREE
{
  number_of_objects 2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 4
  max_distance_to_players 5
  min_distance_group_placement 2
}

create_object OAKTREE
{
  number_of_objects 3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 6
  max_distance_to_players 8
  min_distance_group_placement 1
}

create_object VILLAGER
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
 }

create_object SCOUT
{
  number_of_objects             1
  set_place_for_every_player
  min_distance_to_players       7
  max_distance_to_players       9
 }

if REGICIDE
create_object VILLAGER
{
  number_of_objects             7
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       7
}

create_object KING
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       7
}

create_object CASTLE
{
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    11
}
endif


create_object SHEEP
{
number_of_objects 1
set_place_for_every_player
min_distance_to_players 9
max_distance_to_players 10
}

create_object SHEEP
{
number_of_objects 3
set_loose_grouping
set_gaia_object_only
set_place_for_every_player
min_distance_to_players 9
max_distance_to_players 10
}

create_object SHEEP
{
   number_of_objects 2
   number_of_groups 2
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 14
   max_distance_to_players 30
}

create_object BOAR
{
   number_of_objects 1
number_of_groups 1
set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 16
  max_distance_to_players 22
}

create_object BOAR
{
   number_of_objects 1
number_of_groups 1
set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 16
  max_distance_to_players 22
}

create_object MAP_REVEALER
{
terrain_to_place_on PINE_FOREST
min_distance_group_placement 2
number_of_objects 999
set_gaia_object_only
}



/* NEAR GOLD */

create_object GOLD
{
  number_of_groups 2
  number_of_objects 6
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 11
  max_distance_to_players 18
  min_distance_group_placement 4
  temp_min_distance_group_placement 5
  max_distance_to_other_zones 6
}

/* NEAR STONE */

create_object STONE
{
  number_of_groups 2
  number_of_objects 4
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 12
  max_distance_to_players 21
  min_distance_group_placement 4
  temp_min_distance_group_placement 6
max_distance_to_other_zones 5
}


/* NEAR FORAGE */

create_object FORAGE
{
  number_of_objects 6
  number_of_groups 1
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 10
  max_distance_to_players 15
  min_distance_group_placement 6
  max_distance_to_other_zones 5 
}

create_object DEER
{
number_of_objects 5
number_of_groups 1
	set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 17
  max_distance_to_players 20
}

/* FAR GOLD */

create_object GOLD
{

if TINY_MAP
  number_of_groups 3
  number_of_objects 4
elseif SMALL_MAP
  number_of_groups 4
  number_of_objects 4
elseif MEDIUM_MAP
  number_of_groups 5
  number_of_objects 5
elseif LARGE_MAP
  number_of_groups 7
  number_of_objects 5
elseif HUGE_MAP
  number_of_groups 9
  number_of_objects 5
else
  number_of_groups 10
  number_of_objects 5
endif
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	min_distance_to_players 33
	temp_min_distance_group_placement 19
	min_distance_group_placement 10
max_distance_to_other_zones 6
}


/* FAR STONE */

create_object STONE
{
if TINY_MAP
  number_of_groups 2
  number_of_objects 4
elseif SMALL_MAP
  number_of_groups 3
  number_of_objects 4
elseif MEDIUM_MAP
  number_of_groups 4
  number_of_objects 5
elseif LARGE_MAP
  number_of_groups 5
  number_of_objects 5
elseif HUGE_MAP
  number_of_groups 7
  number_of_objects 5
else
  number_of_groups 8
  number_of_objects 5
endif
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	min_distance_to_players 33
	temp_min_distance_group_placement 19
	min_distance_group_placement 5
max_distance_to_other_zones 6
}


create_object RELIC
{
  number_of_objects 5
  set_gaia_object_only
	min_distance_to_players 25
  min_distance_group_placement 20
}


/* Captain Fin */

create_object DORADO
{
  number_of_objects 19
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones 5
     min_distance_group_placement 4
}

create_object TUNA
{
}
create_object SNAPPER
{
}

create_object MARLIN2
{
  number_of_objects 6
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement 10
  max_distance_to_other_zones 7
}

create_object SHORE_FISH
{
  number_of_objects 25
     set_scaling_to_map_size
     min_distance_group_placement 5
     set_gaia_object_only
}

/* Captain Fin end */

#const FALCON 1056
create_object FALCON
{
  number_of_objects 4
  set_gaia_object_only
  set_scaling_to_map_size
}

/* ************************************** */

<CONNECTION_GENERATION>
create_connect_all_players_land
{
  replace_terrain WATER GRASS2
  replace_terrain MED_WATER GRASS2
  replace_terrain DEEP_WATER GRASS2
  replace_terrain BEACH GRASS2
  terrain_cost WATER 7
  terrain_cost MED_WATER 9
  terrain_cost DEEP_WATER 15
  terrain_cost GRASS 2
  terrain_cost FOREST 7
  terrain_cost PINE_FOREST 7
  terrain_cost BEACH 7
  terrain_size WATER 8 1
  terrain_size MED_WATER 8 1
  terrain_size DEEP_WATER 8 1
  terrain_size BEACH 8 1
  terrain_size GRASS 0 0
  terrain_size FOREST 0 0
  terrain_size PINE_FOREST 0 0
}
