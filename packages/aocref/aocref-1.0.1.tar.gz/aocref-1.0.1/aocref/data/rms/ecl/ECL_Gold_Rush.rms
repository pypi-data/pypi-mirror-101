/* ************ ECL GOLD RUSH ************ */
/*
 * Gold rush for the ECL. This map is used in the qualifiers and in the
 * American region. This version of the map is given a jungle biome for
 * the Mesoamerican region. It also has no ponds.
 */


/* Sling nerf constants. */
#const TRIBUTE_INEFFICIENCY 46
#const BANKING 17
#const COINAGE 23
#const COINAGE_IMP_DISABLER 224


/* ************************************************************************** */
<PLAYER_SETUP>

random_placement

/* Sling nerfs */
effect_percent MOD_RESOURCE TRIBUTE_INEFFICIENCY ATTR_SET 40
effect_amount DISABLE_TECH BANKING ATTR_DISABLE 17
effect_amount DISABLE_TECH COINAGE ATTR_DISABLE 23
effect_amount DISABLE_TECH COINAGE_IMP_DISABLER ATTR_DISABLE 224


/* ************************************************************************** */
<LAND_GENERATION>
base_terrain GRASS

/*  CREATE BADLANDS IN THE MIDDLE */

/* EMPTY LAND */
create_land
{
  terrain_type                     DIRT
  land_percent                     85
  left_border                      15
  right_border                     15
  top_border                       15
  bottom_border                    15
  zone 16
  min_placement_distance           100
}

/* GOOD STUFF */

create_land
{
  terrain_type                     DIRT
  land_percent                     5
  left_border                      40
  right_border                     40
  top_border                       40
  bottom_border                    40
  zone 15
  land_id                          22
 }

create_player_lands
{
  terrain_type                     GRASS
  land_percent                     50
  base_size                        9
  other_zone_avoidance_distance    7
  set_zone_by_team
}


/* ************************************************************************** */
<ELEVATION_GENERATION>

create_elevation        5
{
  base_terrain                GRASS
  number_of_clumps 		14
  number_of_tiles 		2000
  set_scale_by_groups
  set_scale_by_size
}

create_elevation        7
{
  base_terrain                DIRT
  number_of_clumps 		14
  number_of_tiles 		2000
  set_scale_by_groups
  set_scale_by_size
}


/* ************************************************************************** */
<CLIFF_GENERATION>

min_number_of_cliffs 5
max_number_of_cliffs 10
min_length_of_cliff  4
max_length_of_cliff  10
cliff_curliness      10
min_distance_cliffs  3


/* ****************************************************** */
<TERRAIN_GENERATION>

create_terrain JUNGLE
{
  base_terrain                   GRASS
  spacing_to_other_terrain_types 5
  land_percent                   12
  number_of_clumps               10
  set_avoid_player_start_areas
  set_scale_by_groups
}

create_terrain JUNGLE
{
  base_terrain                   GRASS
  spacing_to_other_terrain_types 5
  land_percent                   5
  number_of_clumps               20
  set_avoid_player_start_areas
  set_scale_by_groups
}

create_terrain DESERT
{
  base_terrain                   DIRT
  number_of_clumps               24
  spacing_to_other_terrain_types 1
  land_percent                   8
  set_scale_by_size
}

create_terrain GRASS3
{
  base_terrain                   GRASS
  number_of_clumps               24
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}


/* ****************************************************** */
<OBJECTS_GENERATION>

/* PLAYER START OBJECTS */

create_object TOWN_CENTER
{
  set_place_for_every_player
  group_placement_radius     18
  min_distance_to_players    0
  max_distance_to_players    0
}

create_object VILLAGER
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
 }

create_object SCOUT
{
  number_of_objects             1
  set_place_for_every_player
  min_distance_to_players       7
  max_distance_to_players       9
 }

create_object RELIC
{
   number_of_objects            5
   min_distance_to_players     25
   temp_min_distance_group_placement 20
}

/* SPECIAL STUFF FOR REGICIDE */
/* FORTRESS AND MIGRATION NEED TO BE UPDATED */

if REGICIDE
create_object VILLAGER
{
  number_of_objects             7
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}

create_object KING
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}

create_object CASTLE
{
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    10
}
endif

/* NEAR FORAGE */

create_object FORAGE
{
  number_of_objects          6
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    12
  min_distance_group_placement  6
}

/* BAA BAA */

create_object TURKEY
{
   number_of_objects 4
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    10
   max_distance_to_players    12
   min_distance_group_placement  5
}

create_object TURKEY
{
   number_of_objects 2
   number_of_groups 2
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    14
   max_distance_to_players    30
   min_distance_group_placement  5
}

create_object DEER
{
  number_of_objects          4
  group_variance             1
  group_placement_radius     3
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    14
  max_distance_to_players    30
  min_distance_group_placement  4
}

create_object JAVELINA
{
  number_of_objects          1
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    16
  max_distance_to_players    22
}

create_object JAVELINA
{
  number_of_objects          1
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    16
  max_distance_to_players    22
}

/* NEAR GOLD */

create_object GOLD
{
  number_of_objects          4
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    7
  max_distance_to_players    12
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* FAR GOLD */

create_object GOLD
{
  number_of_objects          3
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    18
  max_distance_to_players    24
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* NEAR STONE */

create_object STONE
{
  number_of_objects          5
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    14
  max_distance_to_players    18
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* FAR STONE */

create_object STONE
{
  number_of_objects          4
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    20
  max_distance_to_players    26
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

create_object JUNGLETREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object JUNGLETREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}

create_object JAGUAR
{
  number_of_objects          2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    30
  max_distance_to_players    40
}


/* RESOURCES IN THE MIDDLE */

create_object GOLD
{
  terrain_to_place_on        DIRT
  number_of_objects          12
  number_of_groups           6
  group_variance             4
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_group_placement  5
  place_on_specific_land_id    22
  max_distance_to_players      14
}

create_object JAGUAR
{
  terrain_to_place_on       DIRT
  number_of_objects          4
  number_of_groups           4
  set_loose_grouping
  set_gaia_object_only
  min_distance_to_players    25
}

/* SCATTERED STONE */

create_object STONE
{
  number_of_groups           1
  number_of_objects          6
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  set_scaling_to_player_number
  min_distance_to_players       20
  max_distance_to_players       45
  min_distance_group_placement  9
}

if LARGE_MAP
create_object STONE
{
  number_of_groups           1
  number_of_objects          6
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object STONE
{
  number_of_groups           2
  number_of_objects          6
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object STONE
{
  number_of_groups           3
  number_of_objects          4
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  9
}
endif

/* ***** PRETTY TREES ********* */

create_object JUNGLETREE
{
  number_of_objects          30
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}

create_object SKELETON
{
   number_of_objects  14
   set_scaling_to_map_size
   set_gaia_object_only
   terrain_to_place_on     DIRT
}
