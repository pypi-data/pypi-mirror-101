/* ************ ECL SAHARA ************ */
/*
 * Sahara map used for the ECL. This map is used in the Africa region.
 * Credits to HJ for greating this map in the SY_MAP_PACK 2014.
 */


/* The original map contains the following credits: */
/* SY_MAP_PACK 2014 made by HJ. Version 1.0, 25 maps */
/* To be used in SY_Nations_Cup 2014 and for community after the event. */

/* Code might look messy some places. I'm not gonna range everything alphabetically. */
/* I'm happy with how it looks right now. */

/* Big thanks to The_Prophet and MadCADer for helping me out! */


/* Sling nerf constants. */
#const TRIBUTE_INEFFICIENCY 46
#const BANKING 17
#const COINAGE 23
#const COINAGE_IMP_DISABLER 224


#const GROUND 6 /* dirt1 */
#const WOODS 13 /* palm */
#const MIDTERR 14 /* desert */
#const PATCH 9 /* more grass less dirt */


/* ************************************************************************** */
<PLAYER_SETUP>

random_placement
ai_info_map_type ARABIA 0 0 0

/* Sling nerfs */
effect_percent MOD_RESOURCE TRIBUTE_INEFFICIENCY ATTR_SET 40
effect_amount DISABLE_TECH BANKING ATTR_DISABLE 17
effect_amount DISABLE_TECH COINAGE ATTR_DISABLE 23
effect_amount DISABLE_TECH COINAGE_IMP_DISABLER ATTR_DISABLE 224

/* Map Revealer Line of Sight */
effect_amount SET_ATTRIBUTE MAP_REVEALER ATTR_LINE_OF_SIGHT 0

/* ************************************************************************** */
<LAND_GENERATION>

base_terrain GROUND

create_player_lands
{
	terrain_type GROUND
	left_border 1
	right_border 1
	top_border 1
	bottom_border 1
	land_percent 25
	base_size 9
	other_zone_avoidance_distance 35
}

/* Middle */
create_land
{
	terrain_type MIDTERR
	land_percent 50
	left_border 20
	right_border 20
	top_border 20
	bottom_border 20
	zone 1
}


/* ************************************************************************** */
<ELEVATION_GENERATION>

create_elevation 1
{
	base_terrain MIDTERR
	number_of_tiles 625
	number_of_clumps 24
	set_scale_by_groups
}

create_elevation 3
{
	base_terrain MIDTERR
	number_of_tiles 625
	number_of_clumps 24
	set_scale_by_groups
}

create_elevation 5
{
	base_terrain MIDTERR
	number_of_tiles 625
	number_of_clumps 24
	set_scale_by_groups
}


/* ************************************************************************** */
<TERRAIN_GENERATION>

create_terrain WOODS {
	set_flat_terrain_only
	base_terrain MIDTERR
	land_percent 1
	number_of_clumps 6
	set_scale_by_groups
}

create_terrain WOODS {
	base_terrain GROUND
	land_percent 12
	number_of_clumps 12
	set_scale_by_groups
	set_avoid_player_start_areas
  clumping_factor 20
}

create_terrain PATCH {
	base_terrain GROUND
	land_percent 8
	number_of_clumps 16
	set_scale_by_size
}

create_terrain GROUND {
	base_terrain MIDTERR
	land_percent 8
	number_of_clumps 24
	set_scale_by_groups
}


/* ************************************************************************** */
<OBJECTS_GENERATION>

/* ********************************************************************** */

/* Resource engine by HJ and The_Prophet. More information at www.AoCZone.net */
/* Start of Engine. */

/* ********************************************************************** */

/* PLAYER START OBJECTS */

create_object TOWN_CENTER
{
  set_place_for_every_player
  group_placement_radius 18
  min_distance_to_players 0
  max_distance_to_players 0
}

create_object VILLAGER
{
  set_place_for_every_player
  min_distance_to_players 6
  max_distance_to_players 6
}

create_object SCOUT
{
  number_of_objects 1
  set_place_for_every_player
  min_distance_to_players 7
  max_distance_to_players 9
}

if REGICIDE
  create_object VILLAGER
  {
    number_of_objects             7
    set_place_for_every_player
    min_distance_to_players       6
    max_distance_to_players       6
  }

  create_object KING
  {
    set_place_for_every_player
    min_distance_to_players       6
    max_distance_to_players       6
  }

  create_object CASTLE
  {
    set_place_for_every_player
    min_distance_to_players    10
    max_distance_to_players    10
  }
endif

create_object MAP_REVEALER {
  number_of_objects 9999
  set_gaia_object_only
  temp_min_distance_group_placement 1
  min_distance_group_placement 2
  terrain_to_place_on PALM_DESERT
}

/* ************* RESOURCES ************* */

create_object SHEEP
{
   number_of_objects 4
   number_of_groups 1
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 9
   max_distance_to_players 11
}

create_object SHEEP
{
   number_of_objects 2
   number_of_groups 1
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 12
   max_distance_to_players 30
}

create_object SHEEP
{
   number_of_objects 2
   number_of_groups 1
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 12
   max_distance_to_players 30
}

create_object DEER
{
  number_of_objects 4
  number_of_groups 1
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 14
  max_distance_to_players 30
}

create_object BOAR
{
  number_of_objects 1
  number_of_groups 1
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 16
  max_distance_to_players 22
}

create_object BOAR
{
  number_of_objects 1
  number_of_groups 1
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 16
  max_distance_to_players 22
}

/* NEAR GOLD */
create_object GOLD
{
  number_of_objects          7
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    12
  max_distance_to_players    16
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* MEDIUM GOLD */
create_object GOLD
{
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    18
  max_distance_to_players    26
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* FAR GOLD */
create_object GOLD
{
  number_of_objects          4
  number_of_groups           1
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       25
  max_distance_to_players       35
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* NEAR STONE */
create_object STONE
{
  number_of_objects          5
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    14
  max_distance_to_players    18
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* FAR STONE */
create_object STONE
{
  number_of_objects          4
  number_of_groups           2
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       20
  max_distance_to_players       26
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* NEAR FORAGE */
create_object FORAGE
{
  number_of_objects          6
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    12
  min_distance_group_placement  6
}

create_object WOLF
{
  number_of_groups          2
  set_loose_grouping
  number_of_objects 1
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement 12
  min_distance_to_players    40
  max_distance_to_players    120
}

create_object WOLF
{
  number_of_objects          2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_group_placement 12
  min_distance_to_players    30
  max_distance_to_players    40
}

/* EXTRA GOLD */

if TINY_MAP
create_object GOLD
{
  number_of_groups           2
  number_of_objects          3
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if SMALL_MAP
create_object GOLD
{
  number_of_groups           2
  number_of_objects          3
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if MEDIUM_MAP
create_object GOLD
{
  number_of_groups           3
  number_of_objects          3
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LARGE_MAP
create_object GOLD
{
  number_of_groups           3
  number_of_objects          3
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object GOLD
{
  number_of_groups           4
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object GOLD
{
  number_of_groups           5
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

/* EXTRA STONE */

if TINY_MAP
create_object STONE
{
  number_of_groups           1
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if SMALL_MAP
create_object STONE
{
  number_of_groups           1
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if MEDIUM_MAP
create_object STONE
{
  number_of_groups           2
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LARGE_MAP
create_object STONE
{
  number_of_groups           3
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object STONE
{
  number_of_groups           4
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object STONE
{
  number_of_groups           5
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

/* straggler trees */
create_object PALM_FOREST_TREE
{
  	number_of_objects             2
  	set_gaia_object_only
  	set_place_for_every_player
  	min_distance_to_players       4
  	max_distance_to_players       5
  	min_distance_group_placement  2
}
create_object PALM_FOREST_TREE
{
  	number_of_objects             3
  	set_gaia_object_only
  	set_place_for_every_player
  	min_distance_to_players       6
 	max_distance_to_players       8
  	min_distance_group_placement  2
}

if LARGE_MAP
create_object FORAGE
{
  number_of_groups           1
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object FORAGE
{
  number_of_groups           2
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object FORAGE
{
  number_of_groups           3
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  9
}
endif

create_object RELIC {
  number_of_objects 5
  min_distance_to_players 25
  min_distance_group_placement 5
  temp_min_distance_group_placement 20
}

/* End of Engine. */
