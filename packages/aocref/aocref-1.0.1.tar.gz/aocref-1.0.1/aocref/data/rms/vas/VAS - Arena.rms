/*

	VAS - Arena by Chrazini
	Created: 07-04-2019
	Version: 1.1

	Theme: Europe
	Style: Land

	Map made for Arena Seasonal:
	https://www.aoezone.net/threads/voobly-arena-seasonal-annoucement-spring-2019.153444/

	Please report bugs or requests on either:
	Steam: https://steamcommunity.com/id/chrazini
	Discord: Chrazini#5687
	AoEZone: https://www.aoezone.net/members/chrazini.48855/

	Changelog 07-04-2019 | Version 1.0
	• Initial release.

	Changelog 07-04-2019 | Version 1.1
	• The hitpoints of walls and gates has been doubled.

*/

#define SPRING_THEME 
if SUMMER_THEME /* Work in Progress */
	#const BASE_TERRAIN 12
	#const SPAWN_TERRAIN 0

	#const FOREST_A 10
	#const FOREST_B 19

	#const TERRAIN_A 9
	#const TERRAIN_B 12

	#const S_STRAGGLER 411
	#const O_STRAGGLER 302

	#const SHRUB 59 

	#const HERDABLE_A 594
	#const UNGULATE_A 65
	#const LUREABLE_A 48
	#const PREDATOR_A 126

	#const BIRD_A 96
	#const BIRD_B 1056
elseif AUTUMN_THEME /* Work in Progress */
	#const BASE_TERRAIN 12
	#const SPAWN_TERRAIN 0

	#const FOREST_A 10
	#const FOREST_B 19

	#const TERRAIN_A 5
	#const TERRAIN_B 9

	#const S_STRAGGLER 411
	#const O_STRAGGLER 302

	#const SHRUB 59 

	#const HERDABLE_A 594
	#const UNGULATE_A 65
	#const LUREABLE_A 48
	#const PREDATOR_A 126

	#const BIRD_A 96
	#const BIRD_B 1056
elseif WINTER_THEME /* Work in Progress */
	#const BASE_TERRAIN 32
	#const SPAWN_TERRAIN 36

	#const FOREST_A 21
	#const FOREST_B 21

	#const TERRAIN_A 34
	#const TERRAIN_B 34

	#const S_STRAGGLER 413
	#const O_STRAGGLER 1054

	#const SHRUB 59 

	#const HERDABLE_A 594
	#const UNGULATE_A 65
	#const LUREABLE_A 48
	#const PREDATOR_A 126

	#const BIRD_A 96
	#const BIRD_B 1056
elseif SPRING_THEME /* Completed */
	#const BASE_TERRAIN 12
	#const SPAWN_TERRAIN 0

	#const FOREST_A 10
	#const FOREST_B 19

	#const TERRAIN_A 9
	#const TERRAIN_B 12

	#const S_STRAGGLER 411
	#const O_STRAGGLER 350	

	#const SHRUB 59 

	#const HERDABLE_A 594
	#const UNGULATE_A 65
	#const LUREABLE_A 48
	#const PREDATOR_A 126

	#const BIRD_A 96
	#const BIRD_B 1056
endif

#define WALL_BUFF
if WALL_BUFF
	#const WALL 117
	#const WALL_HP_TECH 71
	#const GATE_1 64
	#const GATE_2 78
	#const GATE_3 81
	#const GATE_4 487
	#const GATE_5 88
	#const GATE_6 91
	#const GATE_7 95
	#const GATE_8 490
	#const GATE_9 659
	#const GATE_10 661
	#const GATE_11 663
	#const GATE_12 665
	#const GATE_13 667
	#const GATE_14 669
	#const GATE_15 671
	#const GATE_16 673
endif

#define CUSTOM_CONSTANTS
if CUSTOM_CONSTANTS
	#const BUG_PREVENTER 127
	#const STUMP 809
	#const PLANT 818
endif

<PLAYER_SETUP>
	random_placement

	effect_amount SET_ATTRIBUTE BIRD_A ATTR_LINE_OF_SIGHT 0
	effect_amount SET_ATTRIBUTE BIRD_B ATTR_LINE_OF_SIGHT 0
	effect_amount SET_ATTRIBUTE BUG_PREVENTER ATTR_LINE_OF_SIGHT 0

	/* Buffing Walls */
	effect_amount MUL_ATTRIBUTE WALL ATTR_HITPOINTS 2
	effect_amount MUL_ATTRIBUTE GATE_1 ATTR_HITPOINTS 2
	effect_amount MUL_ATTRIBUTE GATE_2 ATTR_HITPOINTS 2
	effect_amount MUL_ATTRIBUTE GATE_3 ATTR_HITPOINTS 2
	effect_amount MUL_ATTRIBUTE GATE_4 ATTR_HITPOINTS 2
	effect_amount MUL_ATTRIBUTE GATE_5 ATTR_HITPOINTS 2
	effect_amount MUL_ATTRIBUTE GATE_6 ATTR_HITPOINTS 2
	effect_amount MUL_ATTRIBUTE GATE_7 ATTR_HITPOINTS 2
	effect_amount MUL_ATTRIBUTE GATE_8 ATTR_HITPOINTS 2
	effect_amount MUL_ATTRIBUTE GATE_9 ATTR_HITPOINTS 2
	effect_amount MUL_ATTRIBUTE GATE_10 ATTR_HITPOINTS 2
	effect_amount MUL_ATTRIBUTE GATE_11 ATTR_HITPOINTS 2
	effect_amount MUL_ATTRIBUTE GATE_12 ATTR_HITPOINTS 2
	effect_amount MUL_ATTRIBUTE GATE_13 ATTR_HITPOINTS 2
	effect_amount MUL_ATTRIBUTE GATE_14 ATTR_HITPOINTS 2
	effect_amount MUL_ATTRIBUTE GATE_15 ATTR_HITPOINTS 2
	effect_amount MUL_ATTRIBUTE GATE_16 ATTR_HITPOINTS 2
	effect_amount DISABLE_TECH WALL_HP_TECH ATTR_DISABLE 71
	ai_info_map_type ARENA 0 0 0

<LAND_GENERATION>
	base_terrain FOREST_A

	create_land
	{
		terrain_type BASE_TERRAIN
		land_percent 60 
		base_size 10
		left_border 18
		right_border 18
		top_border 18
		bottom_border 18
		land_position 50 50
		zone 1
	}

	create_player_lands 
	{
		terrain_type SPAWN_TERRAIN
		land_percent 20
		base_size 12
		left_border 3
		right_border 3
		top_border 3
		bottom_border 3
		border_fuzziness 1
		other_zone_avoidance_distance 30
	}

<TERRAIN_GENERATION>
	create_terrain TERRAIN_A
	{
		base_terrain BASE_TERRAIN
		land_percent 4
		number_of_clumps 4
		clumping_factor 60
		set_scale_by_groups
		spacing_to_other_terrain_types 5
	}

	create_terrain FOREST_B
	{
		base_terrain TERRAIN_A
		land_percent 1
		number_of_clumps 4
		set_avoid_player_start_areas
		set_scale_by_groups
		spacing_to_other_terrain_types 1
	}

	create_terrain TERRAIN_B
	{
		base_terrain BASE_TERRAIN
		land_percent 2
		number_of_clumps 8
		clumping_factor 60
		set_scale_by_groups
	}

<CONNECTION_GENERATION>
	create_connect_all_players_land
	{
		replace_terrain SPAWN_TERRAIN SPAWN_TERRAIN
		replace_terrain BASE_TERRAIN BASE_TERRAIN
		replace_terrain FOREST_A SPAWN_TERRAIN

		terrain_cost FOREST_A 15
		terrain_cost BASE_TERRAIN 1
		terrain_cost SPAWN_TERRAIN 2

		terrain_size FOREST_A 2 1
		terrain_size BASE_TERRAIN 0 0
		terrain_size SPAWN_TERRAIN 0 0
	}

<OBJECTS_GENERATION>
	create_object STUMP
	{
		number_of_objects 6
		set_scaling_to_map_size
		temp_min_distance_group_placement 1
		terrain_to_place_on TERRAIN_A
	}

	create_object PLANT
	{
		number_of_objects 6
		number_of_groups 16
		group_placement_radius 4
		set_scaling_to_map_size
		temp_min_distance_group_placement 4
	}

	create_object RELIC
	{
		number_of_objects 5
		min_distance_to_players 24
		temp_min_distance_group_placement 16
		terrain_to_place_on BASE_TERRAIN
	}

	create_object BUG_PREVENTER
	{
		number_of_objects 4096
		min_distance_group_placement 3
		terrain_to_place_on FOREST_A
	}

	create_object BUG_PREVENTER
	{
		number_of_objects 4096
		min_distance_group_placement 3
		terrain_to_place_on FOREST_B
	}


	create_object TOWN_CENTER
	{
		set_place_for_every_player
		min_distance_to_players 0
		max_distance_to_players 0
	}   

	create_object VILLAGER
	{
		set_place_for_every_player

		if REGICIDE
			number_of_objects 10
		else
			number_of_objects 1
		endif

		min_distance_to_players 6
		max_distance_to_players 7
		temp_min_distance_group_placement 1
	}

	if REGICIDE
		create_object CASTLE
		{
			set_place_for_every_player
			min_distance_to_players 12
			max_distance_to_players 14
			max_distance_to_other_zones 16
		}

		create_object KING
		{
			set_place_for_every_player
			min_distance_to_players 8
			max_distance_to_players 10
		}   

		create_object QUEEN
		{
			set_place_for_every_player
			min_distance_to_players 8
			max_distance_to_players 10
		} 
	else
		create_object SCOUT
		{
			set_place_for_every_player
			min_distance_to_players 7
			max_distance_to_players 9
		}
	endif

	create_object WALL
	{
		number_of_objects 2048
		set_place_for_every_player
		min_distance_to_players 20
		max_distance_to_players 20
	}

	create_object S_STRAGGLER
	{
		set_place_for_every_player
		set_gaia_object_only


		if DEATH_MATCH
			number_of_objects 1
		else
			number_of_objects 2
		endif

		min_distance_to_players 4
		max_distance_to_players 5
		min_distance_group_placement 2
	}
	
	create_object S_STRAGGLER
	{
		set_place_for_every_player
		set_gaia_object_only

		if DEATH_MATCH
			number_of_groups 1
		else
			number_of_objects 2
		endif

		min_distance_to_players 6
		max_distance_to_players 7
		min_distance_group_placement 2
	}
	
	create_object S_STRAGGLER
	{
		set_place_for_every_player
		set_gaia_object_only
		min_distance_to_players 8
		max_distance_to_players 8
		temp_min_distance_group_placement 2
	}

	create_object HERDABLE_A
	{
		set_place_for_every_player
		set_loose_grouping
		min_distance_to_players 10
		max_distance_to_players 10
	}

	create_object HERDABLE_A
	{
		set_place_for_every_player
		set_gaia_object_only
		number_of_objects 3
		set_loose_grouping
		min_distance_to_players 10
		max_distance_to_players 10
	}

	create_object HERDABLE_A
	{
		set_place_for_every_player
		set_gaia_object_only
		number_of_objects 2
		number_of_groups 2
		group_placement_radius 2
		set_loose_grouping
		min_distance_to_players 16
		max_distance_to_players 26
		max_distance_to_other_zones 4
		min_distance_group_placement 2
		temp_min_distance_group_placement 12
	}

	create_object LUREABLE_A
	{
		set_place_for_every_player
		set_gaia_object_only
		number_of_objects 2
		min_distance_to_players 16
		max_distance_to_players 20
		max_distance_to_other_zones 4
		min_distance_group_placement 2
		temp_min_distance_group_placement 12
	}

	create_object UNGULATE_A
	{
		set_place_for_every_player
		set_gaia_object_only
		number_of_objects rnd(3,4)
		set_loose_grouping
		group_placement_radius 2
		min_distance_to_players 16
		max_distance_to_players 16
		min_distance_group_placement 2
	}

	create_object PREDATOR_A
	{
		set_gaia_object_only

		start_random
			percent_chance 75
				number_of_objects 1
			percent_chance 25
				number_of_objects 2
		end_random

		set_loose_grouping
		set_scaling_to_map_size
		temp_min_distance_group_placement 12
		min_distance_to_players 32
		max_distance_to_players 120
	}

	create_object PREDATOR_A
	{
		set_gaia_object_only

		start_random
			percent_chance 75
				number_of_objects 1
			percent_chance 25
				number_of_objects 2
		end_random

		set_loose_grouping
		set_scaling_to_map_size
		temp_min_distance_group_placement 12
		min_distance_to_players 32
		max_distance_to_players 120
	}

	create_object PREDATOR_A
	{
		set_gaia_object_only

		start_random
			percent_chance 75
				number_of_objects 1
			percent_chance 25
				number_of_objects 2
		end_random

		set_loose_grouping
		set_scaling_to_map_size
		temp_min_distance_group_placement 12
		min_distance_to_players 32
		max_distance_to_players 120
	}

	create_object BIRD_A
	{
		number_of_objects rnd(4,6)
		set_scaling_to_map_size
	}

	create_object BIRD_B
	{
		number_of_objects rnd(2,4)
		set_scaling_to_map_size
	}

	create_object SHRUB
	{
		set_place_for_every_player
		set_gaia_object_only
		number_of_objects 6
		set_tight_grouping
		min_distance_to_players 10
		max_distance_to_players 12
		min_distance_group_placement 6
	}

	create_object GOLD
	{
		set_place_for_every_player
		set_gaia_object_only
		number_of_objects 7
		set_tight_grouping
		min_distance_to_players 12
		max_distance_to_players 16
		min_distance_group_placement 10
	}

	create_object GOLD
	{
		set_place_for_every_player
		set_gaia_object_only
		number_of_objects 4
		set_tight_grouping
		min_distance_to_players 18
		max_distance_to_players 22
		min_distance_group_placement 10
	}

	create_object GOLD
	{
		set_place_for_every_player
		set_gaia_object_only
		number_of_objects 4
		set_tight_grouping
		min_distance_to_players 24
		max_distance_to_players 28
		min_distance_group_placement 10
	}

	create_object STONE
	{
		set_place_for_every_player
		set_gaia_object_only
		number_of_objects 5
		set_tight_grouping
		min_distance_to_players 14
		max_distance_to_players 18
		min_distance_group_placement 10
	}

	create_object STONE
	{
		set_place_for_every_player
		set_gaia_object_only
		number_of_objects 4
		set_tight_grouping
		min_distance_to_players 22
		max_distance_to_players 28
		min_distance_group_placement 10
	}

	create_object O_STRAGGLER
	{
		number_of_objects 24
		min_distance_to_players 12
		set_scaling_to_map_size
		temp_min_distance_group_placement 2
	}

	create_object SHRUB
	{
		number_of_objects 5

		if LARGE_MAP
			number_of_groups 1
		elseif HUGE_MAP
			number_of_groups 2
		elseif GIGANTIC_MAP
			number_of_groups 4
		else
			number_of_groups 0
		endif

		set_tight_grouping
		min_distance_to_players 32
		min_distance_group_placement 8

		if LARGE_MAP
			temp_min_distance_group_placement 18
		elseif HUGE_MAP
			temp_min_distance_group_placement 20
		elseif GIGANTIC_MAP
			temp_min_distance_group_placement 24
		endif
	}

	create_object GOLD
	{
		if TINY_MAP
			number_of_objects 3
			number_of_groups 2
		elseif SMALL_MAP
			number_of_objects 3
			number_of_groups 3
		elseif MEDIUM_MAP
			number_of_objects 3
			number_of_groups 4
		elseif LARGE_MAP
			number_of_objects 3
			number_of_groups 5
		elseif HUGE_MAP
			number_of_objects 4
			number_of_groups 4
		elseif GIGANTIC_MAP
			number_of_objects 5
			number_of_groups 4
		endif   

		set_tight_grouping
		min_distance_to_players 34
		min_distance_group_placement 6

		if TINY_MAP
			temp_min_distance_group_placement 10
		elseif SMALL_MAP
			temp_min_distance_group_placement 18
		elseif MEDIUM_MAP
			temp_min_distance_group_placement 20
		elseif LARGE_MAP
			temp_min_distance_group_placement 22
		elseif HUGE_MAP
			temp_min_distance_group_placement 24
		elseif GIGANTIC_MAP
			temp_min_distance_group_placement 28
		endif
	}

	create_object STONE
	{
		if TINY_MAP
			number_of_objects 3
			number_of_groups 2
		elseif SMALL_MAP
			number_of_objects 3
			number_of_groups 3
		elseif MEDIUM_MAP
			number_of_objects 3
			number_of_groups 4
		elseif LARGE_MAP
			number_of_objects 3
			number_of_groups 5
		elseif HUGE_MAP
			number_of_objects 4
			number_of_groups 4
		elseif GIGANTIC_MAP
			number_of_objects 5
			number_of_groups 4
		endif   

		set_tight_grouping
		min_distance_to_players 34
		min_distance_group_placement 6

		if TINY_MAP
			temp_min_distance_group_placement 10
		elseif SMALL_MAP
			temp_min_distance_group_placement 18
		elseif MEDIUM_MAP
			temp_min_distance_group_placement 20
		elseif LARGE_MAP
			temp_min_distance_group_placement 22
		elseif HUGE_MAP
			temp_min_distance_group_placement 24
		elseif GIGANTIC_MAP
			temp_min_distance_group_placement 28
		endif
	}
