/* Tidepool (11-10-17) */

#const ROCKS 40
#const MAP_REVEALER 
#const ATTR_LINE_OF_SIGHT 1
#const GAIA_SET_ATTRIBUTE -1
#const SET_ATTRIBUTE 0

<PLAYER_SETUP>
random_placement
effect_amount SET_ATTRIBUTE MAP_REVEALER ATTR_LINE_OF_SIGHT 0

<LAND_GENERATION>
base_terrain ROCKS
create_land
{
	terrain_type ROCKS
	land_percent 100
	top_border 30
	bottom_border 30
	right_border 30
	left_border 30
}
create_land
{
	terrain_type ROCKS
	land_percent 100
	top_border 30
	bottom_border 30
	right_border 30
	left_border 30
}
create_player_lands
{
	terrain_type GRASS
	land_percent 40
	top_border 10
	bottom_border 10
	right_border 10
	left_border 10
	base_elevation 2 
	base_size 12
	other_zone_avoidance_distance 10
}

<ELEVATION_GENERATION>

<TERRAIN_GENERATION>
create_terrain DESERT
{
	base_terrain ROCKS
	land_percent 100
	number_of_clumps 1000
	height_limits 0 0
}
create_terrain PALM_DESERT
{
	base_terrain GRASS
	land_percent 10
	number_of_clumps 24
	clumping_factor 80
	set_scale_by_groups
	set_avoid_player_start_areas
}
create_terrain WATER
{
	base_terrain DESERT
	land_percent 100
	number_of_clumps 1000
	set_scale_by_groups
	spacing_to_other_terrain_types 3
}
create_terrain PALM_DESERT
{
	base_terrain DESERT
	land_percent 2
	number_of_clumps 48
	set_scale_by_groups
}
create_terrain GRASS3
{
	base_terrain GRASS
	land_percent 10
	number_of_clumps 24
	set_scale_by_groups
}
create_terrain MED_WATER
{
	base_terrain WATER
	land_percent 100
	number_of_clumps 1000
	set_scale_by_groups
	spacing_to_other_terrain_types 5
}

<OBJECTS_GENERATION>
create_object TOWN_CENTER
{
	set_place_for_every_player
	min_distance_to_players 0
	max_distance_to_players 0
}
create_object VILLAGER
{
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 6
}
create_object SCOUT
{
	set_place_for_every_player
	min_distance_to_players 7
	max_distance_to_players 9
}

if REGICIDE /* regicide objects */
create_object VILLAGER
{
  number_of_objects             7
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}
create_object KING
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}
create_object CASTLE
{
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    10
}
endif

if DEATH_MATCH
else
create_object SHEEP /* primary */
{
   	number_of_objects 4
   	set_loose_grouping
   	set_gaia_object_only
   	set_place_for_every_player
   	min_distance_to_players    9
   	max_distance_to_players    11
}
create_object SHEEP /* secondary */
{
   	number_of_objects 2
	number_of_groups 2
   	set_loose_grouping
   	set_gaia_object_only
   	set_place_for_every_player
   	min_distance_to_players    14
   	max_distance_to_players    30
}
create_object WOLF
{
	number_of_objects 2
	set_scaling_to_player_number
	set_gaia_object_only
	min_distance_to_players 30
	max_distance_to_players 40
}

create_object BOAR
{
	number_of_objects 2
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 16
	max_distance_to_players 22
}

create_object DEER
{
	number_of_objects 4
	set_place_for_every_player
	set_gaia_object_only
	set_loose_grouping
	min_distance_to_players 18
	max_distance_to_players 24
}
endif

create_object MAP_REVEALER
{
	number_of_objects 999
	terrain_to_place_on PALM_DESERT
	set_place_for_every_player
	set_gaia_object_only
	min_distance_group_placement 2
}

if DEATH_MATCH
else
create_object PALM_FOREST_TREE /* straggler trees */
{
  	number_of_objects             2
  	set_gaia_object_only
  	set_place_for_every_player
  	min_distance_to_players       4
  	max_distance_to_players       5
  	min_distance_group_placement  2
}
create_object PALM_FOREST_TREE 
{
  	number_of_objects             3
  	set_gaia_object_only
  	set_place_for_every_player
  	min_distance_to_players       6
 	max_distance_to_players       8
  	min_distance_group_placement  2
}
create_object FORAGE 
{
	number_of_objects 6
	set_tight_grouping
	group_placement_radius 4
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 10
	max_distance_to_players 12
	min_distance_group_placement 4
}
endif




create_object GOLD /* primary */
{
  	number_of_objects          7
  	group_placement_radius     3
  	set_tight_grouping
  	set_gaia_object_only
  	set_place_for_every_player
  	min_distance_to_players    12
  	max_distance_to_players    16
  	min_distance_group_placement  7
	max_distance_to_other_zones 5	
}
create_object STONE /* primary */
{
	number_of_objects 5
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players    12
  	max_distance_to_players    16
	min_distance_group_placement  7
	max_distance_to_other_zones 5	
}
create_object GOLD /* secondary */
{
	number_of_objects 5
	set_tight_grouping
	group_placement_radius 3
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 18
	max_distance_to_players 26
	min_distance_group_placement 7
}
create_object STONE /* secondary */
{
	number_of_objects 4
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 20
	max_distance_to_players 26
	min_distance_group_placement 7
	max_distance_to_other_zones 7
}
create_object GOLD /* extra */
{
	number_of_objects 4
	number_of_groups 6
	group_varience 1
	set_tight_grouping
	group_placement_radius 3
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_group_placement 3
	temp_min_distance_group_placement 10
	min_distance_to_players 25
	max_distance_to_other_zones 5
	min_distance_group_placement 5	
}
create_object STONE /* extra */
{
	number_of_objects 4
	number_of_groups 2
	group_varience 1
	set_tight_grouping
	group_placement_radius 3
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_group_placement 3
	temp_min_distance_group_placement 20
	min_distance_to_players 30
	max_distance_to_other_zones 4
}

create_object SNAPPER
{
	number_of_objects 1000
	set_gaia_object_only
	temp_min_distance_group_placement 6
	set_scaling_to_map_size
	max_distance_to_other_zones 4
}
create_object SHORE_FISH
{
	number_of_objects 1000
	set_gaia_object_only
	temp_min_distance_group_placement 7
	set_scaling_to_map_size
}
create_object RELIC 
{
	number_of_objects 5
	min_distance_to_players 25
	set_gaia_object_only
	temp_min_distance_group_placement 25  
}

