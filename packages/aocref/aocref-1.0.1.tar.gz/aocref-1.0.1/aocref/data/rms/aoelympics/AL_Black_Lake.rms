		/* Black Lake (1-30-20) */	
		/* Changes from 1.0: terrain changes for DE compatability */			
						
		start_random				
			percent_chance 50			
				#define COLD		
			percent_chance 50			
				#define MOLD		
		end_random				
						
		if COLD				
			#const GROUND 0			
			#const WOODS 19				
			#const MIDPATCH 25			
			#const PATCH 3			
			#const CTERR 37			
		elseif MOLD				
			#const GROUND 12			
			#const WOODS 17					
			#const MIDPATCH 39			
			#const PATCH 9			
			#const CTERR 2			
		endif				
						
		<PLAYER_SETUP>				
		random_placement				
						
		<LAND_GENERATION>				
		base_terrain WOODS				
		create_player_lands				
		{				
			terrain_type GROUND			
			land_percent 60			
			top_border 5			
			bottom_border 5			
			right_border 5			
			left_border 5			
			base_size 9			
			other_zone_avoidance_distance 7			
		}				
		create_land				
		{				
			terrain_type CTERR			
			land_percent 100			
			top_border 30			
			bottom_border 30			
			right_border 30			
			left_border 30			
		}				
		create_land				
		{				
			terrain_type CTERR			
			land_percent 100			
			top_border 30			
			bottom_border 30			
			right_border 30			
			left_border 30			
		}				
						
		<ELEVATION_GENERATION>				
		create_elevation 3				
		{				
			base_terrain WOODS			
			number_of_tiles 1250			
			number_of_clumps 32			
			set_scale_by_groups			
		}				
		create_elevation 3				
		{				
			base_terrain GROUND			
			number_of_tiles 1250			
			number_of_clumps 32			
			set_scale_by_groups			
		}				
		if TINY_MAP				
			create_elevation 6			
		else				
			create_elevation 7			
		endif				
		{				
			base_terrain CTERR			
			if DE_AVAILABLE
				number_of_tiles 100000
			else
				number_of_tiles 10000
			endif		
			number_of_clumps 1000			
			set_scale_by_groups			
			if TINY_MAP			
				spacing 2		
			elseif SMALL_MAP			
				spacing 2		
			elseif MEDIUM_MAP			
				spacing 2		
			elseif LARGE_MAP			
				spacing 2		
			elseif HUGE_MAP			
				spacing 2		
			elseif GIGANTIC_MAP			
				spacing 2		
			endif			
		}				
						
		<TERRAIN_GENERATION>				
		create_terrain WATER				
		{				
			base_terrain CTERR			
			land_percent 100			
			number_of_clumps 1000			
			set_scale_by_groups			
			if TINY_MAP			
				height_limits 6 6		
			else			
				height_limits 7 7		
			endif			
			spacing_to_other_terrain_types 1			
		}				
		create_terrain GROUND				
		{				
			base_terrain CTERR			
			land_percent 100			
			number_of_clumps 1000			
			set_scale_by_groups			
		}				
		create_terrain WATER				
		{				
			base_terrain GROUND			
			land_percent 1			
			number_of_clumps 4			
			set_scale_by_groups			
			set_avoid_player_start_areas			
			set_flat_terrain_only			
			spacing_to_other_terrain_types 1			
		}				
		create_terrain WOODS				
		{				
			base_terrain GROUND			
			land_percent 15			
			number_of_clumps 24			
			set_scale_by_groups			
			set_avoid_player_start_areas			
			spacing_to_other_terrain_types 3			
		}								
		create_terrain PATCH				
		{				
			base_terrain GROUND			
			land_percent 7			
			number_of_clumps 24			
			set_scale_by_groups			
		}				
		create_terrain LEAVES				
		{				
			base_terrain GROUND			
			land_percent 7			
			number_of_clumps 24			
			set_scale_by_groups			
		}				
		create_terrain MED_WATER				
		{				
			base_terrain WATER			
			land_percent 100			
			number_of_clumps 100			
			set_scale_by_groups			
			spacing_to_other_terrain_types 5			
		}				
		create_terrain DEEP_WATER				
		{				
			base_terrain MED_WATER			
			land_percent 100			
			number_of_clumps 100			
			set_scale_by_groups			
			spacing_to_other_terrain_types 5			
		}				
						
		<CONNECTION_GENERATION>				
		create_connect_all_lands				
		{				
			replace_terrain WOODS GROUND			
			terrain_size WOODS 1 0			
			terrain_size GROUND 1 0			
			terrain_cost WOODS 3				
		}				
		create_connect_teams_lands  				
		{				
			replace_terrain WOODS GROUND			
			replace_terrain GROUND MIDPATCH			
			replace_terrain PATCH MIDPATCH			
			replace_terrain LEAVES MIDPATCH			
			terrain_size WOODS 1 0			
			terrain_size GROUND 1 0			
			terrain_size MIDPATCH 0 0			
			terrain_cost WOODS 10			
			terrain_cost GROUND 3			
			terrain_cost WATER 5			
			terrain_cost PATCH 3			
			terrain_cost LEAVES 3				
			terrain_cost CTERR 10			
			terrain_cost MIDPATCH 1			
		}				
						
		<OBJECTS_GENERATION>				
		create_object TOWN_CENTER				
		{				
			set_place_for_every_player			
			min_distance_to_players 0			
			max_distance_to_players 0			
		}				
		create_object VILLAGER				
		{				
			set_place_for_every_player			
			min_distance_to_players 6			
			max_distance_to_players 6			
		}				
		create_object SCOUT				
		{				
			set_place_for_every_player			
			min_distance_to_players 7			
			max_distance_to_players 9			
		}				
		create_object GOLD /* primary */				
		{				
			number_of_objects          7			
			group_placement_radius     3			
			set_tight_grouping			
			set_gaia_object_only			
			set_place_for_every_player			
			min_distance_to_players    12			
			max_distance_to_players    16			
			min_distance_group_placement  7			
		}				
		create_object STONE /* primary */				
		{				
			number_of_objects 5			
			group_placement_radius 3			
			set_tight_grouping			
			set_gaia_object_only			
			set_place_for_every_player			
			min_distance_to_players    12			
			max_distance_to_players    16			
			min_distance_group_placement  7			
		}				
		create_object GOLD /* secondary */				
		{				
			number_of_objects 6			
			set_tight_grouping			
			group_placement_radius 3			
			set_place_for_every_player			
			set_gaia_object_only			
			min_distance_to_players 18			
			max_distance_to_players 26			
			min_distance_group_placement 7			
			max_distance_to_other_zones 7			
		}				
		create_object STONE /* secondary */				
		{				
			number_of_objects 5			
			group_placement_radius 2			
			set_tight_grouping			
			set_gaia_object_only			
			set_place_for_every_player			
			min_distance_to_players 20			
			max_distance_to_players 26			
			min_distance_group_placement 7			
			max_distance_to_other_zones 7			
		}				
		create_object GOLD /* tertiary */				
		{				
			number_of_objects 4			
			number_of_groups 2			
			group_variance 1			
			set_tight_grouping			
			group_placement_radius 3			
			set_gaia_object_only			
			set_place_for_every_player			
			min_distance_group_placement 3			
			temp_min_distance_group_placement 20			
			min_distance_to_players 25			
			max_distance_to_players 35			
			max_distance_to_other_zones 7			
		}				
		if DEATH_MATCH				
		else				
		if COLD				
		create_object SHEEP /* primary */				
		else				
		create_object TURKEY				
		endif				
		{				
			number_of_objects 4			
			set_loose_grouping			
			set_gaia_object_only			
			set_place_for_every_player			
			min_distance_to_players    9			
			max_distance_to_players    11			
		}				
		if COLD				
		create_object SHEEP /* secondary */				
		else				
		create_object TURKEY				
		endif				
		{				
			number_of_objects 2			
			number_of_groups 2			
			set_loose_grouping			
			set_gaia_object_only			
			set_place_for_every_player			
			min_distance_to_players    14			
			max_distance_to_players    30			
		}				
		if COLD				
		create_object WOLF				
		else				
		create_object JAGUAR				
		endif						
		{				
			number_of_objects 3			
			set_place_for_every_player			
			set_gaia_object_only			
			min_distance_to_players 30			
			max_distance_to_players 40			
		}					
		create_object FORAGE 				
		{				
			number_of_objects 6			
			set_tight_grouping			
			group_placement_radius 4			
			set_gaia_object_only			
			set_place_for_every_player			
			min_distance_to_players 10			
			max_distance_to_players 12			
		}				
		create_object BOAR				
		{				
			number_of_objects 2			
			set_place_for_every_player			
			set_gaia_object_only			
			min_distance_to_players 16			
			max_distance_to_players 22			
		}				
		create_object BOAR /* extra */				
		{				
			number_of_objects 3			
			group_variance 1			
			set_loose_grouping			
			set_gaia_object_only			
			set_place_for_every_player			
			min_distance_to_players 35			
			min_distance_group_placement 5					
		}				
		create_object DEER				
		{				
			number_of_objects 4			
			set_place_for_every_player			
			set_gaia_object_only			
			set_loose_grouping			
			min_distance_to_players 18			
			max_distance_to_players 24			
		}				
		if COLD				
		create_object PINETREE /* straggler trees */				
		else				
		create_object PALM_FOREST_TREE				
		endif				
		{				
			number_of_objects             2			
			set_gaia_object_only			
			set_place_for_every_player			
			min_distance_to_players       4			
			max_distance_to_players       5			
			min_distance_group_placement  2			
		}				
		if COLD				
		create_object PINETREE /* straggler trees */				
		else				
		create_object PALM_FOREST_TREE				
		endif				
		{				
			number_of_objects             3			
			set_gaia_object_only			
			set_place_for_every_player			
			min_distance_to_players       6			
			max_distance_to_players       8			
			min_distance_group_placement  2			
		}				
		endif				
		if COLD				
		create_object PINETREE 				
		else				
		create_object PALM_FOREST_TREE				
		endif				
		{				
			number_of_objects 10          			
			set_gaia_object_only			
			set_scaling_to_map_size			
			temp_min_distance_group_placement  5			
			max_distance_to_players       15		
		}				
		if COLD				
		create_object PINETREE 				
		else				
		create_object PALM_FOREST_TREE				
		endif				
		{				
			number_of_objects 20			
			set_gaia_object_only			
			set_scaling_to_map_size			
			temp_min_distance_group_placement  5			
			min_distance_to_players 10			
			terrain_to_place_on GROUND			
		}				
		create_object RELIC 				
		{				
			number_of_objects 5			
			min_distance_to_players 25			
			set_gaia_object_only			
			temp_min_distance_group_placement 25  			
		}				
		create_object SHORE_FISH				
		{				
			number_of_objects 5			
			set_gaia_object_only			
			temp_min_distance_group_placement 2			
			set_scaling_to_map_size			
		}								
		create_object RUINS				
		{				
			number_of_objects 1			
			set_gaia_object_only			
			set_scaling_to_map_size									
			temp_min_distance_group_placement 10	
			min_distance_to_players 15
		}				
		create_object ROMAN_RUINS				
		{				
			number_of_objects 1			
			set_gaia_object_only			
			set_scaling_to_map_size									
			temp_min_distance_group_placement 10	
			min_distance_to_players 15		
		}				
		create_object TUNA				
		{				
			number_of_objects 100			
			temp_min_distance_group_placement 5			
			max_distance_to_other_zones 3			
		}				
		create_object SHORE_FISH				
		{				
			number_of_objects 100			
			temp_min_distance_group_placement 5			
		}				
		if REGICIDE /* regicide objects */				
		create_object VILLAGER				
		{				
		  number_of_objects             7				
		  set_place_for_every_player				
		  min_distance_to_players       6				
		  max_distance_to_players       6				
		}				
		create_object KING				
		{				
		  set_place_for_every_player				
		  min_distance_to_players       6				
		  max_distance_to_players       6				
		}				
		create_object CASTLE				
		{				
		  set_place_for_every_player				
		  min_distance_to_players    10				
		  max_distance_to_players    10				
		}				
		endif				