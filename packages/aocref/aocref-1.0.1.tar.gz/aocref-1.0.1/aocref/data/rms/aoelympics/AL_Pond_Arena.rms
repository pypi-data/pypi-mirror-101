/* Pondarena (9-23-17) */				
/* Changes from 1.0: reconstucted script reducing trapped resources */				
/* Changes from 1.1: added space in back of players' bases */				
				
start_random				
	percent_chance 50			
		#define OAK		
	percent_chance 50			
		#define PINE		
end_random				
				
if OAK				
	#const GROUND 0			
	#const WOODS 10			
	#const PATCH 6			
elseif PINE				
	#const GROUND 9			
	#const WOODS 19			
	#const PATCH 3			
endif				
				
<PLAYER_SETUP>				
random_placement				
				
<LAND_GENERATION>				
base_terrain GROUND				
create_player_lands				
{				
	terrain_type GROUND			
	land_percent 10			
	top_border 1			
	bottom_border 1			
	right_border 1			
	left_border 1			
	base_size 15			
	other_zone_avoidance_distance 5			
	base_elevation 1			
}				
create_land				
{				
	terrain_type WATER			
	land_percent 100			
	top_border 35			
	bottom_border 35			
	right_border 35			
	left_border 35			
	other_zone_avoidance_distance 5			
}				
				
<ELEVATION_GENERATION>				
				
<TERRAIN_GENERATION>				
create_terrain WOODS				
{				
	base_terrain GROUND			
	land_percent 100			
	number_of_clumps 100			
	set_scale_by_groups			
	if TINY_MAP			
		spacing_to_other_terrain_types 27		
	elseif SMALL_MAP			
		spacing_to_other_terrain_types 32		
	elseif MEDIUM_MAP			
		spacing_to_other_terrain_types 37		
	elseif LARGE_MAP			
		spacing_to_other_terrain_types 42		
	elseif HUGE_MAP			
		spacing_to_other_terrain_types 47		
	elseif GIGANTIC_MAP			
		spacing_to_other_terrain_types 52		
	endif			
	height_limits 0 0			
}				
create_terrain MED_WATER				
{				
	base_terrain WATER			
	land_percent 100			
	spacing_to_other_terrain_types 3			
}				
create_terrain DEEP_WATER				
{				
	base_terrain MED_WATER			
	land_percent 100			
	spacing_to_other_terrain_types 5			
}				
create_terrain WOODS				
{				
	base_terrain GROUND			
	land_percent 5			
	number_of_clumps 12			
	set_scale_by_groups			
	set_avoid_player_start_areas			
}				
create_terrain PATCH				
{				
	base_terrain GROUND			
	land_percent 15			
	number_of_clumps 24			
	set_scale_by_groups			
}				
				
<OBJECTS_GENERATION>				
create_object TOWN_CENTER				
{				
	set_place_for_every_player			
	min_distance_to_players 0			
	max_distance_to_players 0			
}				
create_object VILLAGER				
{				
	set_place_for_every_player			
	min_distance_to_players 6			
	max_distance_to_players 6			
}				
create_object SCOUT				
{				
	set_place_for_every_player			
	min_distance_to_players 7			
	max_distance_to_players 9			
}				
if DEATH_MATCH				
create_object FORTIFIED_WALL				
{				
  number_of_objects             9999				
  set_place_for_every_player				
  min_distance_to_players 20				
  max_distance_to_players 20				
}				
else				
create_object WALL				
{				
  number_of_objects             9999				
  set_place_for_every_player				
  min_distance_to_players 20				
  max_distance_to_players 20				
}				
endif				
create_object GOLD /* primary */				
{				
  	number_of_objects          7			
  	group_placement_radius     3			
  	set_tight_grouping			
  	set_gaia_object_only			
  	set_place_for_every_player			
  	min_distance_to_players    12			
  	max_distance_to_players    16			
  	min_distance_group_placement  7			
}				
create_object STONE /* primary */				
{				
	number_of_objects 5			
	group_placement_radius 3			
	set_tight_grouping			
	set_gaia_object_only			
	set_place_for_every_player			
	min_distance_to_players    12			
  	max_distance_to_players    16			
	min_distance_group_placement  7			
}				
create_object GOLD /* secondary */				
{				
	number_of_objects 5			
	set_tight_grouping			
	group_placement_radius 3			
	set_place_for_every_player			
	set_gaia_object_only			
	min_distance_to_players 18			
	max_distance_to_players 26			
	min_distance_group_placement 7			
	max_distance_to_other_zones 7			
}				
create_object STONE /* secondary */				
{				
	number_of_objects 4			
	group_placement_radius 2			
	set_tight_grouping			
	set_gaia_object_only			
	set_place_for_every_player			
	min_distance_to_players 20			
	max_distance_to_players 26			
	min_distance_group_placement 7			
	max_distance_to_other_zones 7			
}				
create_object GOLD /* extra */				
{				
	number_of_objects 4			
	number_of_groups 3			
	group_variance 1			
	set_tight_grouping			
	group_placement_radius 3			
	set_gaia_object_only			
	set_scaling_to_map_size			
	min_distance_group_placement 3			
	temp_min_distance_group_placement 20			
	min_distance_to_players 30			
}				
create_object STONE /* extra */				
{				
	number_of_objects 4			
	number_of_groups 2			
	group_variance 1			
	set_tight_grouping			
	group_placement_radius 3			
	set_gaia_object_only			
	set_scaling_to_map_size			
	min_distance_group_placement 3			
	temp_min_distance_group_placement 20			
	min_distance_to_players 30			
}				
if DEATH_MATCH				
else				
create_object SHEEP /* primary */				
{				
   	number_of_objects 4			
   	set_loose_grouping			
   	set_gaia_object_only			
   	set_place_for_every_player			
   	min_distance_to_players    9			
   	max_distance_to_players    11			
}				
create_object SHEEP /* secondary */				
{				
   	number_of_objects 2			
	number_of_groups 2			
   	set_loose_grouping			
   	set_gaia_object_only			
   	set_place_for_every_player			
   	min_distance_to_players    14			
   	max_distance_to_players    30			
}				
create_object FORAGE 				
{				
	number_of_objects 6			
	set_tight_grouping			
	group_placement_radius 4			
	set_gaia_object_only			
	set_place_for_every_player			
	min_distance_to_players 10			
	max_distance_to_players 12			
}				
create_object BOAR				
{				
	number_of_objects 2			
	set_place_for_every_player			
	set_gaia_object_only			
	min_distance_to_players 16			
	max_distance_to_players 22			
}				
create_object DEER				
{				
	number_of_objects 4			
	set_place_for_every_player			
	set_gaia_object_only			
	set_loose_grouping			
	min_distance_to_players 18			
	max_distance_to_players 24			
}				
if OAK				
	create_object OAKTREE /* straggler trees */			
elseif PINE				
	create_object PINETREE			
endif				
{				
  	number_of_objects             2			
  	set_gaia_object_only			
  	set_place_for_every_player			
  	min_distance_to_players       4			
  	max_distance_to_players       5			
  	min_distance_group_placement  2			
}				
if OAK				
	create_object OAKTREE 			
elseif PINE				
	create_object PINETREE			
endif				
{				
  	number_of_objects             3			
  	set_gaia_object_only			
  	set_place_for_every_player			
  	min_distance_to_players       6			
 	max_distance_to_players       8			
  	min_distance_group_placement  2			
}				
endif				
create_object RELIC 				
{				
	number_of_objects 5			
	min_distance_to_players 25			
	set_gaia_object_only			
	temp_min_distance_group_placement 25  			
}				
create_object SALMON				
{				
	number_of_objects 100			
	set_scaling_to_map_size			
	temp_min_distance_group_placement 6			
	max_distance_to_other_zones 2			
}				
create_object SHORE_FISH				
{				
	number_of_objects 10			
	set_scaling_to_map_size			
	temp_min_distance_group_placement 10			
}				
if REGICIDE /* regicide objects */				
create_object VILLAGER				
{				
  number_of_objects             7				
  set_place_for_every_player				
  min_distance_to_players       6				
  max_distance_to_players       6				
}				
create_object KING				
{				
  set_place_for_every_player				
  min_distance_to_players       6				
  max_distance_to_players       6				
}				
create_object CASTLE				
{				
  set_place_for_every_player				
  min_distance_to_players    10				
  max_distance_to_players    10				
}				
endif				